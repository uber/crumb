/*
 * Copyright (c) 2018. Uber Technologies
 *                            
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *                            
 *   http://www.apache.org/licenses/LICENSE-2.0
 *                            
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'com.diffplug.gradle.spotless'
}

allprojects {
  buildscript {
    repositories {
      jcenter()
      google()
    }
  }

  repositories {
    jcenter()
    google()
  }
}

subprojects {
  afterEvaluate {
    if (project.getPlugins().hasPlugin('com.android.application') ||
        project.getPlugins().hasPlugin('com.android.library')) {

      android.lintOptions {
        // The lint task fails on travis CI's machines, but isn't necessary for these samples either
        tasks.lint.enabled = false
      }
    }
  }

  apply from: rootProject.file('gradle/dependencies.gradle')
  apply plugin: 'com.diffplug.gradle.spotless'
  spotless {
    format 'misc', {
      target '**/*.md', '**/.gitignore'

      trimTrailingWhitespace()
      indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
      endWithNewline()
    }
    // Not yet, pending https://github.com/diffplug/spotless/issues/142
    //    kotlin {
    //      target "**/*.kt"
    //      ktlint(deps.versions.ktlint)
    //      licenseHeaderFile rootProject.file('spotless/copyright.kt')
    //    }
    java {
      target "**/src/main/**/com/uber/*.java"
      googleJavaFormat(deps.versions.gjf)
      licenseHeaderFile rootProject.file('spotless/copyright.java')
      removeUnusedImports()
    }
    groovyGradle {
      target '**/*.gradle'
    }
  }
}

apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/publish-docs.gradle'
